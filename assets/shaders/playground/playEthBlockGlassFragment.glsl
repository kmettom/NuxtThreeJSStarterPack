precision highp float;

#define MAX_GLASS 6

varying vec2 vUv;
uniform sampler2D uSceneTexture;
uniform vec4 uBlocks[MAX_GLASS];
uniform int uBlockCount;
uniform vec2 uMeshSize;

float boxRadius = 20.0;

vec4 glassPass(vec2 vUv, vec2 uv, vec4 baseColor, vec4 rect) {
    vec2 glassCenter = vec2(0.5) + rect.xy / uMeshSize;
    vec2 glassHalfUv = rect.zw / uMeshSize;

    vec2 m2 = (vUv - glassCenter) / glassHalfUv;
    float roundedBox = pow(abs(m2.x), boxRadius) + pow(abs(m2.y), boxRadius);

    float rb1 = clamp((1.00 - roundedBox) * 8.0, 0.0, 1.0);
    float rb2 = clamp((0.95 - roundedBox) * 16.0, 0.0, 1.0) -
    clamp((0.90 - roundedBox) * 16.0, 0.0, 1.0);
    float rb3 = clamp((1.50 - roundedBox) * 2.0, 0.0, 1.0)
    - clamp((1.00 - roundedBox) * 2.0, 0.0, 1.0);

    float transition = smoothstep(0.0, 1.0, rb1 + rb2);
    if (transition <= 0.0) return baseColor;

    vec2 lensVUv = glassCenter + (vUv - glassCenter) * (1.0 - roundedBox * 0.5);
    vec2 lensUv = lensVUv;

    vec4 blurred = vec4(0.0);
    float total = 0.0;
    for (float x = -4.0; x <= 4.0; x++) {
        for (float y = -4.0; y <= 4.0; y++) {
            vec2 off = vec2(x, y) * 0.5 / uMeshSize;
            blurred += texture2D(uSceneTexture, lensUv + off);
            total += 1.0;
        }
    }
    blurred /= total;

    vec2 m2uv = vUv - glassCenter;
    float gradient =
    clamp((clamp(m2uv.y, 0.0, 0.2) + 0.1) * 0.5, 0.0, 1.0) +
    clamp((clamp(-m2uv.y, -1000.0, 0.2) * rb3 + 0.1) * 0.5, 0.0, 1.0);

    vec4 lighting = clamp(
    blurred
    - vec4(vec3(rb1 * gradient * 0.95), 0.0)
    - vec4(vec3(rb2 * 0.3), 0.0),
    0.0, 1.0
    );
    float glassOpacity = clamp(0.85 + transition * 0.9, 0.0, 1.0);
    return mix(baseColor, lighting, glassOpacity);
}

void main() {
    vec2 uv = vUv;
    vec4 color = texture2D(uSceneTexture, uv);

    for (int i = 0; i < MAX_GLASS; i++) {
        if (i >= uBlockCount) break;
        color = glassPass(vUv, uv, color, uBlocks[i]);
    }

    gl_FragColor = color;
}